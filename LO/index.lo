lo.index{
  -- Implement an index tree structure

  import lo.core.
  import lo.list.

  public
  contract all m,k,v ~~ /*equality[k] |: */ map[m ->> k,v] .. {
      present:(m,k,v){}.
      remove:(m,k) => m.
      put:(m,k,v) => m.
    }.

  public
  type all k,v ~~ /* equality[k] |: */treeMap[k,v] ::= 
      trEmpty
    | trLeaf(integer,list[(k,v)])
    | trNode(integer,integer,treeMap[k,v],treeMap[k,v]).

  public
  implementation all k,v ~~ equality[k] |: map[treeMap[k,v] ->> k,v] .. {
    present(M,K,V) :- lookIn(hash(K),M,K,V).
    remove(M,K) => rmve(hash(K),K,M).
    put(M,K,V) => insrt(K,V,M).
  }

  public
  implementation all k,v ~~ equality[k] |: additive[treeMap[k,v]] .. {
    M1+M2 => mergeTree(M1,M2).
    M1-M2 => subtractTree(M1,M2).
  }

  lookIn: all k,v ~~ equality[k] |: (integer,treeMap[k,v],k,v){}.
  lookIn(H,trLeaf(H,Els),K,V) :-- findMember(K,Els,V).
  lookIn(H,trNode(Msk,Ln,Left,Right),K,V) :: commonMask(H,Ln)==Msk :--
    (nthBit(H,Ln) ? lookIn(H,Right,K,V) | lookIn(H,Left,K,V)).

  look: all k,v ~~ equality[k] |: (k,treeMap[k,v]) => option[v].
  look(K,T) :: lookIn(hash(K),T,K,V) => some(V).
  look(_,_) => none.

  findMember:all k,v ~~ equality[k] |: (k,list[(k,v)],v){}.
  findMember(K,[(Ky,V),.._],V) :- K==Ky.
  findMember(K,[_,..L],V) :- findMember(K,L,V).

  insrt:all k,v ~~ equality[k] |: (k,v,treeMap[k,v])=>treeMap[k,v].
  insrt(K,V,T) => mergeTree(T,trLeaf(hash(K),[(K,V)])).

  mergeTree:all k,v ~~ equality[k] |: (treeMap[k,v],treeMap[k,v])=>treeMap[k,v].
  mergeTree(trEmpty,T) => T.
  mergeTree(T,trEmpty) => T.
  mergeTree(T1,T2) => mergeNodes(T1,T2).

  mergeLeafs:all k,v ~~ equality[k] |: (treeMap[k,v],treeMap[k,v])=>treeMap[k,v].
  mergeLeafs(trLeaf(H,L1),trLeaf(H,L2)) => trLeaf(H,merge(L1,L2)).
  mergeLeafs(T1::T1 = trLeaf(H1,L1),T2::T2=trLeaf(H2,L2)) ::
    ( CML = commonMaskLen(H1,H2,HashLen),
      CM = commonMask(H1,CML)) =>
      (nthBit(H1,CML) ?
        trNode(CM,CML,T2,T1) |
        trNode(CM,CML,T1,T2)).

  merge: all k,v ~~ equality[k] |: (list[(k,v)],list[(k,v)])=>list[(k,v)].
  merge([],L) => L.
  merge([(K,V),..L1],L) :: isMember((K,_),L) => merge(L1,L).
  merge([E,..L],L1) => [E,..merge(L,L1)].

  mergeNodes:all k,v ~~ equality[k] |: (treeMap[k,v],treeMap[k,v])=>treeMap[k,v].
  mergeNodes(T1::T1=trLeaf(_,_),T2::T2=trLeaf(_,_)) => mergeLeafs(T1,T2).
  mergeNodes(T1::T1=trNode(Msk1,Ln1,L1,R1),T2::T2=trLeaf(Msk2,_)) ::
    ( CML = min(commonMaskLen(Msk1,Msk2,HashLen),Ln1),
      CM = commonMask(Msk1,CML)) =>
      (CML<Ln1 ?
        ( nthBit(Msk2,CML) ?
          trNode(CM,CML,T1,T2) |
          trNode(CM,CML,T2,T1))  |
        ( nthBit(Msk2,CML) ?
          trNode(CM,CML,L1,mergeNodes(R1,T2)) |
          trNode(CM,CML,mergeNodes(L1,T2),R1))).
  mergeNodes(T1::T1=trLeaf(Msk1,_), T2::T2=trNode(Msk2,Ln2,L2,R2)) ::
    ( CML = min(commonMaskLen(Msk1,Msk2,HashLen),Ln2),
      CM = commonMask(Msk2,CML)) =>
      (CML < Ln2 ?
        ( nthBit(Msk1,CML) ?
          trNode(CM,CML,T1,T2) |
          trNode(CM,CML,T2,T1))  |
        ( nthBit(Msk1,CML) ?
          trNode(CM,CML,L2,mergeNodes(R2,T1)) |
          trNode(CM,CML,mergeNodes(L2,T1),R2)
          )).
  mergeNodes(T1::T1=trNode(Msk1,Ln1,L1,R1),T2::T2=trNode(Msk2,Ln2,L2,R2)) ::
    ( CML = min(min(commonMaskLen(Msk1,Msk2,HashLen),Ln1),Ln2),
      CM = commonMask(Msk1,CML)) =>
      (CML < Ln1 ?
        (nthBit(Msk2,CML) ?
          trNode(CM,CML,L1,mergeNodes(R1,T2)) |
          trNode(CM,CML,mergeNodes(L1,T2),R1))  |
      CML < Ln2 ?
        (nthBit(Msk1,CML) ?
          trNode(CM,CML,L2,mergeNodes(T1,R2)) |
          trNode(CM,CML,mergeNodes(L2,T1),R2))  |
        trNode(CM,CML,mergeNodes(L1,L2),mergeNodes(R1,R2))
      ).

  rmve:all k,v ~~ equality[k] |: (integer,k,treeMap[k,v]) => treeMap[k,v].
  rmve(_,_,trEmpty) => trEmpty.
  rmve(H,K,trLeaf(H,L)) => reformLeaf(H,subtract((K,_),L)).
  rmve(H,K,T::T=trNode(M,Ln,L,R)) ::
    CM = commonMask(H,Ln) =>
    ( CM = M ?
      ( nthBit(H,Ln) ?
        reformNode(trNode(M,Ln,L,rmve(H,K,R))) |
        reformNode(trNode(M,Ln,rmve(H,K,L),R))
      ) | 
      T). -- not present

  reformLeaf:all  k,v ~~ equality[k] |: (integer,list[(k,v)]) => treeMap[k,v].
  reformLeaf(H,[]) => trEmpty.
  reformLeaf(H,L) => trLeaf(H,L).

  reformNode:all k,v ~~ equality[k] |: (treeMap[k,v]) => treeMap[k,v].
  reformNode(trNode(_,_,trEmpty,R)) => R.
  reformNode(trNode(_,_,L,trEmpty)) => L.
  reformNode(N) => N.

  subtractTree:all k,v ~~ equality[k] |: (treeMap[k,v],treeMap[k,v])=>treeMap[k,v].
  subtractTree(trEmpty,T) => T.
  subtractTree(T,trEmpty) => T.
  subtractTree(T1,T2) => subtractNodes(T1,T2).

  subtractNodes:all k,v ~~ equality[k] |: (treeMap[k,v],treeMap[k,v])=>treeMap[k,v].
  subtractNodes(T1,trLeaf(_,Leaves)) => subtractLeafs(T1,Leaves).
  subtractNodes(T1,trNode(Msk2,Ln2,L2,R2)) =>
    subtractNodes(subtractNodes(T1,L2),R2).

  subtractLeafs:all k,v ~~ equality[k] |: (treeMap[k,v],list[(k,v)])=>treeMap[k,v].
  subtractLeafs(T,[]) => T.
  subtractLeafs(T,[(K,_),..Lvs]) =>
    subtractLeafs(rmve(hash(K),K,T),Lvs).

  public
  implementation all k,v ~~ display[k], display[v] |: display[treeMap[k,v]] .. {
    disp(Tree) => ssSeq([ss("{"),displayElements(Tree),ss("}")]).
  }

  displayElements:all k,v ~~ display[k], display[v] |: (treeMap[k,v]) => ss.
  displayElements(trEmpty) => ssSeq([]).
  displayElements(trLeaf(_,Lvs)) => ssSeq(displayLeaves(Lvs)).
  displayElements(trNode(_,_,Lft,Rgt)) => ssSeq([displayElements(Lft),displayElements(Rgt)]).

  displayLeaves:all k,v ~~ display[k], display[v] |: (list[(k,v)]) => list[ss].
  displayLeaves([]) => [].
  displayLeaves([(K,V),..More]) => [disp(K),ss("->"),disp(V),ss(". "),..displayLeaves(More)].

  HashLen:integer.
  HashLen = 64.
      
  commonMaskLen:(integer,integer,integer) => integer.
  commonMaskLen(H1,H2,C) :: ( C>0, \+H1==H2) => commonMaskLen(_blsr(H1,1),_blsr(H2,1),C-1).
  commonMaskLen(_,_,C) => C.

  commonMask:(integer,integer)=>integer.
  commonMask(_,0) => 0.
  commonMask(M1,ML) :: CML=HashLen-ML => _band(_blsl(_blsr(-1,CML),CML),M1).

  nthBit:(integer,integer){}.
  nthBit(X,N) :- _nthb(X,63-N).
}