/*
  Bootstrap program, this program is loaded automatically
  when the L&O engine is started. Its main function is to set up the
  environment and then load and execute the program mentioned in the command line

  Copyright (c) 2016. Francis G. McCabe

  Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
  except in compliance with the License. You may obtain a copy of the License at

  http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software distributed under the
  License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
  KIND, either express or implied. See the License for the specific language governing
  permissions and limitations under the License.
*/
lo.boot{
  import lo.
  import lo.options.
  import lo.either.
  import lo.repo.
  import lo.repo.file.
  import lo.uri.

  bootOptions <~ { repo:string. wd:string. }
  bootOptions : (string,string) <=> bootOptions.
  bootOptions(R,W) <= {
    repo = R.
    wd = W.
  }

  repoOption:optionsProcessor[bootOptions].
  repoOption <= {
    shortForm = "-r".
    alternatives = [].
    usage = "-r dir -- directory of code repository".
    validator = some(isDir).
    setOption(R,bootOptions(_,W)) => bootOptions(R,W).
  }

  wdOption:optionsProcessor[bootOptions].
  wdOption <= {
    shortForm = "-w".
    alternatives = [].
    usage = "-w dir -- override working directory".
    validator = some(isDir).
    setOption(W,bootOptions(R,_)) => bootOptions(R,W).
  }

  public __boot:(){}.
  __boot() :-
    WD = cwd(),
    handleCmdLineOpts(processOptions(_command_line(),[repoOption,wdOption],bootOptions(WD,WD))).

  handleCmdLineOpts:(either[(bootOptions,list[string]),string]){}.
  handleCmdLineOpts(either((Opts,[Top,..Args]))) :-
    RD = resolveUri(parseUri(cwd()),parseUri(Opts.repo)),
    Repo = (coreRepo,openRepository(RD)),
    importPkgs([parsePkgName(Top)],[],_,Repo),
    invokeMain(Top,Args).

  importPkgs:all r ~~ repository[r] |: (list[pkg],list[pkg],list[pkg],r){}.
  importPkgs([],Ld,Ld,_).
  importPkgs([P,..L],Ld,Ldx,R) :-
    _logmsg("importing \(P)"),
    importPkg(P,R,Ld,Imps),
    importPkgs(Imps,[P,..Ld],Ldx,R),
    initialize(P).

  importPkg:all r ~~ repository[r] |: (pkg,r,list[pkg],list[pkg]){}.
  importPkg(P,_,Ld,[]) :- P in Ld.
  importPkg(P,R,Ld,Imps//(((Pk,V))=>pkgUp(Pk,V))) :-
    loadFromRepo(R,P,"code",Code),
    Imps = _install_pkg(Code).

  pkgUp:(string,string) => pkg.
  pkgUp(P,"*") => pkg(P,defltVersion).
  pkgUp(P,V) => pkg(P,vers(V)).

  initialize:(pkg){}.
  initialize(pkg(P,_)) :-
    Pred = P+"@init",
    ( _defined(Pred,0) ?
      _call(Pred,0,[]) |
      logMsg("No init for \(P)")).

  invokeMain:(string,list[string]){}.
  invokeMain(Top,Args) :-
    Pred = Top+"@_main",
    ( _defined(Pred,1) ?
      logMsg("Starting ..."),
      _call(Pred,1,[Args]) |
      logMsg("No main program: \(Top)") ).

  private delayHandler:(list[(){}]){}. -- private, but known to the run-time
  delayHandler([]).
  delayHandler([H,..L]) :- H()!, delayHandler(L).
}
