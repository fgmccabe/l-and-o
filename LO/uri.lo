lo.uri {
  -- Utilities to help parse and manipulate URIs
  -- This code is an attempt to directly implement the specification in RFC 2396

  import lo.

  public type uri ::= absUri(string,rsrcName,query) | relUri(rsrcName,query).
  public type rsrcName ::= netRsrc(authority,resourcePath) | localRsrc(resourcePath).
  public type resourcePath ::= absPath(list[string]) | relPath(list[string]).

  public type authority ::= server(option[userInfo],host).

  public type userInfo ::= user(string).

  public type host ::= hostPort(string,string) | host(string).

  public type query ::= qry(string) | noQ.

  public parseUri:(string) => uri.
  parseUri(S) => U :- uriParse(U) %% explode(S).

  public uriParse:(uri) --> list[integer].
  uriParse(absUri(Scheme,Hier,Query)) --> absoluteUri(Scheme,Hier,Query).
  uriParse(relUri(Hier,Query)) --> relativeUri(Hier,Query).

  absoluteUri:(string,rsrcName,query) --> list[integer].
  absoluteUri(Scheme,Hier,Query) --> scheme(Scheme), ":", hierPart(Hier,Query).

  scheme:(string) --> list[integer].
  scheme(implode(Scheme)) --> grabScheme(Scheme), ":"+. 

  grabScheme:(list[integer]) --> list[integer].
  grabScheme([C,..S]) --> alpha(C), alphaStar(S).

  alphaStar:(list[integer]) --> list[integer].
  alphaStar([C,..S]) --> (alpha(C) | digit(C) | plus(C) | minus(C) | dot(C)), alphaStar(S).
  alphaStar([]) --> [].

  relativeUri:(rsrcName,query) --> list[integer].
  relativeUri(Hier,Query) --> netPath(Hier), query(Query).
  relativeUri(localRsrc(Rsrc),Query) --> absoluteRsrc(Rsrc), query(Query).
  relativeUri(localRsrc(Rsrc),Query) --> relativeRsrc(Rsrc), query(Query).

  hierPart:(rsrcName,query) --> list[integer].
  hierPart(Hier,Query) --> netPath(Hier), query(Query).
  hierPart(localRsrc(Rsrc),Query) --> absoluteRsrc(Rsrc), query(Query).

  absoluteRsrc:(resourcePath) --> list[integer].
  absoluteRsrc(absPath(Segments)) --> "/", pathSegments(Segments).

  relativeRsrc:(resourcePath) --> list[integer].
  relativeRsrc(relPath(Segments)) --> pathSegments(Segments).

  pathSegments:(list[string]) --> list[integer].
  pathSegments([Seg,..M]) --> segment(Seg), ("/", pathSegments(M) | M=[]).

  segment:(string) --> list[integer].
  segment(implode(C)) --> pChars(C,R), parameters(R).

  pChars:(list[integer],list[integer]) --> list[integer].
  pChars([C,..M],R) --> unreserved(C), pChars(M,R).
  pChars([0c%,U,L,..M],R) --> escaped(U,L), pChars(M,R).
  pChars([C,..M],R) --> [C], { pChar(C)} , pChars(M,R).
  pChars(X,X) --> [].

  pChar:(integer){}.
  pChar(0c:).
  pChar(0c@).
  pChar(0c&).
  pChar(0c=).
  pChar(0c+).
  pChar(0c$).
  pChar(0c<).

  parameters:(list[integer]) --> list[integer].
  parameters(P) --> parameter(P,S), parameters(S).
  parameters([]) --> [].

  parameter:(list[integer],list[integer]) --> list[integer].
  parameter([0c;,..P],M) --> ";", pChars(P,M).

  query:(query) --> list[integer].
  query(qry(implode(Q))) --> "?", uricStar(Q).
  query(noQ) --> eof.

  uricStar:(list[integer]) --> list[integer].
  uricStar(S) --> uric(S,M), uricStar(M).
  uricStar([]) --> [].

  netPath:(rsrcName) --> list[integer].
  netPath(netRsrc(A,P)) --> "//", authority(A), optAbsolutePath(P).

  authority:(authority) --> list[integer].
  authority(server(some(user(implode(U))),H)) --> userInfo(U), "@", hostNamePort(H).
  authority(server(none,H)) --> hostNamePort(H).

  userInfo:(list[integer]) --> list[integer].
  userInfo(U) --> userStar(U).

  userStar:(list[integer]) --> list[integer].
  userStar([C,..S]) --> unreserved(C), userStar(S).
  userStar([0c%,U,L,..S]) --> escaped(U,L), userStar(S).
  userStar([0c$,..S]) --> "$", userStar(S).
  userStar([0c,,..S]) --> ",", userStar(S).
  userStar([0c;,..S]) --> ";", userStar(S).
  userStar([0c:,..S]) --> ":", userStar(S).
  userStar([0c&,..S]) --> "&", userStar(S).
  userStar([0c=,..S]) --> "=", userStar(S).
  userStar([0c+,..S]) --> "+", userStar(S).
  userStar([]) --> "@"+.

  hostNamePort:(host) --> list[integer].
  hostNamePort(hostPort(H,P)) --> hostName(H), ":", port(P).
  hostNamePort(host(H)) --> hostName(H), \+":".

  hostName:(string) --> list[integer].
  hostName(implode(H)) --> alphaDashStar(H)!.

  alphaDashStar:(list[integer]) --> list[integer].
  alphaDashStar([C,..S]) --> (alpha(C) | digit(C) | minus(C) | dot(C)) , alphaDashStar(S).
  alphaDashStar([]) --> [].

  port:(string) --> list[integer].
  port(implode(P)) --> digits(P).

  optAbsolutePath:(resourcePath) --> list[integer].
  optAbsolutePath(P) --> absoluteRsrc(P).
  optAbsolutePath(relPath([])) --> "?"+.
  optAbsolutePath(relPath([])) --> eof.

  plus:(integer) --> list[integer].
  plus(0c+) --> "+".

  minus:(integer) --> list[integer].
  minus(0c-) --> "-".

  dot:(integer) --> list[integer].
  dot(0c.) --> "-".

  alpha:(integer)-->list[integer].
  alpha(C) --> [C] , {isLowAlpha(C)|isUpAlpha(C)}.

  alphaNum:(integer) --> list[integer].
  alphaNum(C) --> alpha(C).
  alphaNum(C) --> digit(C).

  digit:(integer) --> list[integer].
  digit(X) --> [X], {isDigit(X)}.

  digits:(list[integer]) --> list[integer].
  digits([C,..S]) --> digit(C), digits(S).
  digits([]) --> \+digit(_).

  hex:(integer) --> list[integer].
  hex(C) --> digit(C).
  hex(C) --> [C], { isHexDigit(C) }.

  uric:(list[integer],list[integer]) --> list[integer].
  uric([C,..M],M) --> (reserved(C) | unreserved(C)).
  uric([0c%,U,L,..M],M) --> escaped(U,L).

  reserved:(integer) --> list[integer].
  reserved(C) --> [C], { isReserved(C) }.

  unreserved:(integer) --> list[integer].
  unreserved(C) --> alphaNum(C).
  unreserved(C) --> mark(C).

  mark:(integer) --> list[integer].
  mark(C) --> [C], { isMark(C) }.

  escaped:(integer,integer) --> list[integer].
  escaped(U,L) --> "%", hex(U), hex(L).

  delim:(integer) --> list[integer].
  delim(C) --> [C], { isDelim(C) }.

  isLowAlpha:(integer){}.
  isLowAlpha(0ca).
  isLowAlpha(0cb).
  isLowAlpha(0cc).
  isLowAlpha(0cd).
  isLowAlpha(0ce).
  isLowAlpha(0cf).
  isLowAlpha(0cg).
  isLowAlpha(0ch).
  isLowAlpha(0ci).
  isLowAlpha(0cj).
  isLowAlpha(0ck).
  isLowAlpha(0cl).
  isLowAlpha(0cm).
  isLowAlpha(0cn).
  isLowAlpha(0co).
  isLowAlpha(0cp).
  isLowAlpha(0cq).
  isLowAlpha(0cr).
  isLowAlpha(0cs).
  isLowAlpha(0ct).
  isLowAlpha(0cu).
  isLowAlpha(0cv).
  isLowAlpha(0cw).
  isLowAlpha(0cx).
  isLowAlpha(0cy).
  isLowAlpha(0cz).

  isUpAlpha:(integer){}.
  isUpAlpha(0cA).
  isUpAlpha(0cB).
  isUpAlpha(0cC).
  isUpAlpha(0cD).
  isUpAlpha(0cE).
  isUpAlpha(0cF).
  isUpAlpha(0cG).
  isUpAlpha(0cH).
  isUpAlpha(0cI).
  isUpAlpha(0cJ).
  isUpAlpha(0cK).
  isUpAlpha(0cL).
  isUpAlpha(0cM).
  isUpAlpha(0cN).
  isUpAlpha(0cO).
  isUpAlpha(0cP).
  isUpAlpha(0cQ).
  isUpAlpha(0cR).
  isUpAlpha(0cS).
  isUpAlpha(0cT).
  isUpAlpha(0cU).
  isUpAlpha(0cV).
  isUpAlpha(0cW).
  isUpAlpha(0cX).
  isUpAlpha(0cY).
  isUpAlpha(0cZ).

  isDigit:(integer){}.
  isDigit(0c0).
  isDigit(0c1).
  isDigit(0c2).
  isDigit(0c3).
  isDigit(0c4).
  isDigit(0c5).
  isDigit(0c6).
  isDigit(0c7).
  isDigit(0c8).
  isDigit(0c9).

  isHexDigit:(integer){}.
  isHexDigit(0ca).
  isHexDigit(0cb).
  isHexDigit(0cc).
  isHexDigit(0cd).
  isHexDigit(0ce).
  isHexDigit(0cf).
  isHexDigit(0cA).
  isHexDigit(0cB).
  isHexDigit(0cC).
  isHexDigit(0cD).
  isHexDigit(0cE).
  isHexDigit(0cF).

  isReserved:(integer){}.
  isReserved(0c;).
  isReserved(0c/).
  isReserved(0c?).
  isReserved(0c:).
  isReserved(0c@).
  isReserved(0c&).
  isReserved(0c=).
  isReserved(0c+).
  isReserved(0c$).
  isReserved(0c,).

  isMark:(integer){}.
  isMark(0c-).
  isMark(0c_).
  isMark(0c.).
  isMark(0c!).
  isMark(0c~).
  isMark(0c*).
  isMark(0c').
  isMark(0c().
  isMark(0c)).

  isDelim:(integer){}.
  isDelim(0c<).
  isDelim(0c>).
  isDelim(0c#).
  isDelim(0c%).
  isDelim(0c").

  -- Resolve a url against a base. The base must be an absolute URI, either net or local.
  public 
  resolveUri:(uri,uri) => uri.
  resolveUri(_,U) => U :- absUri(_,_,_).=U.
  resolveUri(absUri(Scheme,Base,_),relUri(Path,Query)) => absUri(Scheme,resolvePath(Base,Path),Query).

  resolvePath:(rsrcName,rsrcName)=>rsrcName.
  resolvePath(_,netRsrc(A,P)) => netRsrc(A,P).
  resolvePath(netRsrc(A,_),localRsrc(absPath(P))) => netRsrc(A,absPath(P)).
  resolvePath(netRsrc(A,absPath(B)),localRsrc(relPath(P))) => netRsrc(A,absPath(edit(P,drop(reverse(B))))).
  resolvePath(localRsrc(_),localRsrc(absPath(P))) => localRsrc(absPath(P)).
  resolvePath(localRsrc(absPath(B)),localRsrc(relPath(P))) => localRsrc(absPath(edit(P,drop(reverse(B))))).

  edit: (list[string],list[string]) => list[string].
  edit([".",..Segs],R) => edit(Segs,R).
  edit(["..",..Segs],[_,..R]) => edit(Segs,R).
  edit(Segs,R) => reverse(R)<>Segs.

  public 
  implementation display[uri]..{
    disp(absUri(Scheme,Rsrc,Query)) => ssSeq([ss(Scheme),ss(":"),dispRsrc(Rsrc),dispQuery(Query)]).
    disp(relUri(Rsrc,Query)) => ssSeq([dispRsrc(Rsrc),dispQuery(Query)]).

    private dispQuery:(query)=>ss.
    dispQuery(noQ) => ssSeq([]).
    dispQuery(qry(Q)) => ssSeq([ss("?"),ss(Q)]).
  }

  dispRsrc:(rsrcName)=>ss.
  dispRsrc(netRsrc(H,P)) => ssSeq([dispAuthority(H),dispPath(P)]).
  dispRsrc(localRsrc(P)) => dispPath(P).

  dispAuthority:(authority)=>ss.
  dispAuthority(server(none,H)) => dispHost(H).
  dispAuthority(server(some(U),H)) => ssSeq([dispUser(U),ss("@"),dispHost(H)]).

  dispUser:(userInfo)=>ss.
  dispUser(user(U)) => ss(U).

  dispHost:(host) => ss.
  dispHost(hostPort(H,P)) => ssSeq([ss(H),ss(":"),ss(P)]).
  dispHost(host(H)) => ss(H).

  dispPath:(resourcePath)=>ss.
  dispPath(absPath(Segs)) => ssSeq([ss("/"),..dispSegs(Segs)]).
  dispPath(relPath(Segs)) => ssSeq(dispSegs(Segs)).

  dispSegs:(list[string]) => list[ss].
  dispSegs([]) => [].
  dispSegs([S]) => [ss(S)].
  dispSegs([S,..M]) => [ss(S),ss("/"),..dispSegs(M)].

  public
  getUriPath:(uri)=>string.
  getUriPath(absUri(_,Pth,_)) => formatSS(dispRsrc(Pth)).
  getUriPath(relUri(Pth,_)) => formatSS(dispRsrc(Pth)).
}