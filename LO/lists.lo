lo.list{
  -- implement standard list functions
  import lo.core.
  import lo.collection.
  import lo.coerce.

  public (<>):all t ~~ (list[t],list[t])=>list[t].
  [] <> X => X.
  [E,..X] <> Y => [E,..X<>Y].

  public (in):all t ~~ (t,list[t]){}.
  X in [X,.._].
  X in [_,..Y] :- X in Y.

  public multiCat:all x ~~ (list[list[x]]) => list[x].
  multiCat([]) => [].
  multiCat([E,..L]) => cat(E,L).

  cat:all x ~~ (list[x], list[list[x]]) => list[x].
  cat([],M) => multiCat(M).
  cat([E,..L],M) => [E,..cat(L,M)].

  public reverse:all t ~~ (list[t]) => list[t].
  reverse(X) => rev(X,[]).

  private
  rev:all t ~~ (list[t],list[t]) => list[t].
  rev([],R) => R.
  rev([E,..L],R) => rev(L,[E,..R]).

  public drop:all t ~~ (list[t]) => list[t].
  drop([_,..L]) => L.

  public isMember:all t ~~ (t,list[t]){}.
  isMember(E,[E,.._]).
  isMember(E,[_,..L]) :- isMember(E,L).

  public subtract:all t ~~ (t,list[t])=>list[t].
  subtract(_,[]) => [].
  subtract(E,[E,..R]) => R.
  subtract(E,[X,..R]) => [E,..subtract(E,R)].

  public
  length:all t ~~ (list[t]) => integer.
  length([]) => 0.
  length([_,..L]) => length(L)+1.

  public implementation all e ~~ folding[list[e]->>e] .. {
    foldLeft(f,x,[]) => x.
    foldLeft(f,x,[e,..l]) => foldLeft(f,f.apply(x,e),l).

    foldRight(f,x,[]) => x.
    foldRight(f,x,[e,..l]) => f.apply(e,foldRight(f,x,l)).
  }

  public implementation all e,f ~~ coercion[e,f] |: coercion[list[e],list[f]] .. {
    _coerce(Sx) => coerceList(Sx).
  }

  coerceList:all e,f ~~ coercion[e,f] |: (list[e]) => list[f].
  coerceList([]) => [].
  coerceList([e,..l]) => [_coerce(e),..coerceList(l)].

}