lo.comp.code.analyse{
  import lo.
  import lo.sort.

  import lo.comp.code.registers.

  import lo.comp.escapes.
  import lo.comp.term.

  public partitionGoals:(list[pred],list[pred]) => list[list[pred]].
  partitionGoals([],[]) => [].
  partitionGoals([],P) => [reverse(P)].
  partitionGoals([G,..Rest],P) => partitionGoals(Rest,[G,..P]) :- isSafeGoal(G).
  partitionGoals([G,..Rest],P) => [reverse([G,..P]),..partitionGoals(Rest,[])].

  isSafeGoal:(pred){}.
  isSafeGoal(neck).
  isSafeGoal(fail).
  isSafeGoal(unfy(_,_)).

  /* This is where we analyse the structure of a clause in order to perform register and local variable allocations */

  type occForm ::= hArg | hTerm | lArg | lTerm | oArg | oTerm.

  -- Usedto keep track of occurrences.
  type occMap ::= occ(string,list[(occForm,integer,integer)]).

  public varAnalysis:(list[term],list[term],list[list[pred]]) => (map[string,varDesc],integer,varSet).
  varAnalysis(Q,Args,Parts) => allocateVars(usageTable(OccTble,size(Args),Parts),maxHeadArity(Parts)) :-
    OccTble = values(occurranceTable(Q,Args,Parts)).

  occurranceTable:(list[term],list[term],list[list[pred]]) => map[string,occMap].
  occurranceTable(Q,Args,Parts) => findOccsInParts(Parts,findOccsInHead(Args,occMap(Q,[])),1).

  occMap:(list[term],map[string,occMap]) => map[string,occMap].
  occMap([],M) => M.
  occMap([varbl(Nm),..Q],M) => occMap(Q,M[Nm->occ(Nm,[])]).

  -- Find occurrances of variables to help decide how to allocate them.
  findOccsInHead:(list[term],map[string,occMap]) => map[string,occMap].
  findOccsInHead(Args,M) => findOccsInArgs(Args,M,1,1,hArg,hTerm).

  findOccsInArgs:(list[term],map[string,occMap],integer,integer,occForm,occForm) => map[string,occMap].
  findOccsInArgs([],M,_,_,_,_) => M.
  findOccsInArgs([t,..l],M,Gp,Ix,Key,iKey) => findOccsInArgs(l,findOccsInTerm(t,M,Gp,Ix,Key,iKey),Gp,Ix+1,Key,iKey).

  findOccsInParts:(list[list[pred]],map[string,occMap],integer) => map[string,occMap].
  findOccsInParts([P1,..P],M,Gno) => findOccsInParts(P,findOccsInPart(P1,M,Gno),Gno+1).
  findOccsInParts([],M,_) => M.

  findOccsInPart:(list[pred],map[string,occMap],integer) => map[string,occMap].
  findOccsInPart([],M,Gp) => M.
  findOccsInPart([G,..l],M,Gp) => findOccsInPart(l,findOccsInGoal(G,M,Gp),Gp).

  findOccsInGoal:(pred,map[string,occMap],integer) => map[string,occMap].
  findOccsInGoal(call(Op,Args),M,Gp) => findOccsInArgs(Args,findOccsInTerm(Op,M,Gp,0,oArg,oTerm),Gp,1,lArg,lTerm).
  findOccsInGoal(ecall(Op,Args),M,Gp) => findOccsInArgs(Args,M,Gp,1,lArg,lTerm).
  findOccsInGoal(ocall(C,L,T),M,Gp) => findOccsInArgs([C,L,T],M,Gp,1,lArg,lTerm).
  findOccsInGoal(unfy(L,R),M,Gp) => findOccsInTerm(R,findOccsInTerm(L,M,Gp,1,oArg,oTerm),Gp,2,oArg,oTerm).
  findOccsInGoal(except(T),M,Gp) => findOccsInTerm(T,M,Gp,1,lArg,lTerm).
  findOccsInGoal(neck,M,_) => M.
  findOccsInGoal(fail,M,_) => M.

  findOccsInTerm:(term,map[string,occMap],integer,integer,occForm,occForm) => map[string,occMap].
  findOccsInTerm(varbl(Nm),M,Gp,Ix,Key,iKey) => updateOccurrence(Nm,M,Key,Gp,Ix) :- \+isEscape(Nm).
  findOccsInTerm(anon,M,_,_,_,_) => M.
  findOccsInTerm(T,M,Gp,Ix,Key,_) => updateOccurrence("",M,Key,Gp,Ix) :- isGroundTerm(T). -- special for ground
  findOccsInTerm(cons(_,Els),M,Gp,Ix,_,iKey) => findOccsInTerms(Els,M,Gp,Ix,iKey,iKey).

  findOccsInTerms:(list[term],map[string,occMap],integer,integer,occForm,occForm) => map[string,occMap].
  findOccsInTerms([],M,_,_,_,_) => M.
  findOccsInTerms([t,..l],M,Gp,Ix,Key,iKey) => findOccsInTerms(l,findOccsInTerm(t,M,Gp,Ix,Key,iKey),Gp,Ix,Key,iKey).

  updateOccurrence:(string,map[string,occMap],occForm,integer,integer) => map[string,occMap].
  updateOccurrence(Nm,M,Key,Gp,Ix) => M[Nm->occ(Nm,[(Key,Gp,Ix),..Occs])] :- present(M,Nm,occ(Nm,Occs)).
  updateOccurrence(Nm,M,Key,Gp,Ix) => M[Nm->occ(Nm,[(Key,Gp,Ix)])].

  type usageMap ::= usage(string,occMap,addr,set[integer],set[integer],set[integer]).

  implementation display[usageMap] <= {
    disp(U) => dispUsage(U).
  }

  dispUsage:(usageMap) => ss.
  dispUsage(usage(Nm,Occs,Addr,In,Avoid,Conflict)) =>
      ssSeq([ss("usage for "),ss(Nm),ss(":"),disp(Addr),ss(", inset="),disp(In),ss(", avoid set="),disp(Avoid),ss(" conflict set="),disp(Conflict)]).

  -- Analyse how variables are used.
  usageTable:(list[occMap],integer,list[list[pred]]) => list[usageMap].
  usageTable(Occs,Arity,Parts) => buildRegUsage(Occs,Occs,Parts,Arity,[]).

  buildRgUsage:(occMap,list[occMap],list[list[pred]],integer,list[usageMap]) => list[usageMap].
  buildRgUsage(occ("",_),_,_,_,Out) => Out.
  buildRgUsage(occ(Nm,Occs),T,Parts,Arity,Out) => [usage(Nm,occ(Nm,Occs),aReg(-1),Use,Avoid,conflict(Occs,Ar)),..Out] :-
    Nm\="",
    classifyVr(Occs,aReg(_),some(G)),
    Use = occupiedRegisters(Occs),
    Avoid = avoidRegisters(T,G,Use,nullSet),
    Ar = (nthEl(Parts,G-1,GPart) ? max(Arity,maxArity(GPart)) | Arity).
  buildRgUsage(occ(Nm,Occs),T,Parts,Arity,Out) =>
      [usage(Nm,occ(Nm,Occs),Addr,nullSet,nullSet,nullSet),..Out] :-
    classifyVr(Occs,Addr,_).

  buildRegUsage:(list[occMap],list[occMap],list[list[pred]],integer,list[usageMap]) => list[usageMap].
  buildRegUsage([],_,_,_,U) => U.
  buildRegUsage([O,..L],T,Parts,Arity,Out) =>
    buildRegUsage(L,T,Parts,Arity,buildRgUsage(O,T,Parts,Arity,Out)).

  maxArity:(list[pred]) => integer.
  maxArity([]) => 0.
  maxArity([C,..L]) => max(predArity(C),maxArity(L)).

  maxHeadArity:(list[list[pred]]) => integer.
  maxHeadArity([]) => 0.
  maxHeadArity([P,.._]) => maxArity(P).

  occupiedRegisters:(list[(occForm,integer,integer)]) => set[integer].
  occupiedRegisters(Occs) => foldRight(registerUsed,nullSet,Occs).

  registerUsed:((occForm,integer,integer),set[integer]) => set[integer].
  registerUsed((hArg,_,Ix),Regs) => addMem(Regs,Ix).
  registerUsed((lArg,_,Ix),Regs) => addMem(Regs,Ix).
  registerUsed(_,Regs) => Regs.

  -- This would be better with lambdas
  avoidRegisters:(list[occMap],integer,set[integer],set[integer]) => set[integer].
  avoidRegisters([],Gp,Used,SoFar) => SoFar.
  avoidRegisters([occ(_,Occs),..O],Gp,Used,SoFar) =>
    avoidRegisters(O,Gp,Used,pickRegisters(Occs,Gp,Used,SoFar)).

  pickRegisters:(list[(occForm,integer,integer)],integer,set[integer],set[integer]) => set[integer].
  pickRegisters([],_,_,SoFar) => SoFar.
  pickRegisters([(hArg,Gp,Rg),..Occs],Gp,Used,SoFar) => pickRegisters(Occs,Gp,Used,addMem(SoFar,Rg)) :- \+ Rg in Used.
  pickRegisters([(lArg,Gp,Rg),..Occs],Gp,Used,SoFar) => pickRegisters(Occs,Gp,Used,addMem(SoFar,Rg)) :- \+ Rg in Used.
  pickRegisters([_,..Occs],Gp,Used,SoFar) => pickRegisters(Occs,Gp,Used,SoFar).

  conflict:(list[(occForm,integer,integer)],integer) => set[integer].
  conflict(Ocs,MaxAr) => nonArgUses(1,MaxAr,Ocs)+laterUses(Ocs,Ocs).

  nonArgUses:(integer,integer,list[(occForm,integer,integer)]) => set[integer].
  nonArgUses(Rg,Lim,_) => nullSet :- Rg>Lim.
  nonArgUses(Rg,Lim,Ocs) => addMem(nonArgUses(Rg+1,Lim,Ocs),Rg) :- \+ (lArg,_,Rg) in Ocs.
  nonArgUses(Rg,Lim,Ocs) => nonArgUses(Rg+1,Lim,Ocs).

  laterUses:(list[(occForm,integer,integer)],list[(occForm,integer,integer)]) => set[integer].
  laterUses([],_) => nullSet.
  laterUses([(lTerm,_,Rg),..O],Ocs) => addMem(laterUses(O,Ocs),Rg) :- (lTerm,_,GG) in Ocs, GG>Rg.
  laterUses([(oTerm,_,Rg),..O],Ocs) => addMem(laterUses(O,Ocs),Rg) :- (oTerm,_,GG) in Ocs, GG>Rg.
  laterUses([_,..O],Ocs) => laterUses(O,Ocs).

  classifyVr:(list[(occForm,integer,integer)],addr,option[integer]){}.
  classifyVr([],voidAddr,none). -- should never happen if our quantification is good.
  classifyVr([_],voidAddr,none). -- singleton variable
  classifyVr([(_,Gp,_),..Rest],aReg(-1),some(Gp)) :- (_,G,_) in Rest *> G=Gp. -- -1 means not yet allocated
  classifyVr(Occs,yReg(-1,true),none) :- ((hTerm,_,_) in Occs | (hArg,_,_) in Occs | (oTerm,_,_) in Occs | (lTerm,_,_) in Occs)!.
  classifyVr(_,yReg(-1,false),none).

  classifyVar:(occMap) => addr.
  classifyVar(occ(_,Ocss)) => Addr :- classifyVr(Occs,Addr,_).

  maxRegister:(pred)=>integer.
  maxRegister(call(_,Args)) => size(Args).
  maxRegister(ecall(_,Args)) => size(Args).
  maxRegister(ocall(_,_,_)) => 3.
  maxRegister(except(_)) => 1.
  maxRegister(unfy(_,_)) => 2.
  maxRegister(_) => 0.

  collectVarUsage:(map[string,occMap],map[string,(set[integer],set[integer],set[integer])]) =>
      map[string,(occMap,addr,set[integer],set[integer],set[integer])].
  collectVarUsage(M,Usage) => foldMap(M,collEntry(Usage),[]).

  collEntry:(map[string,(set[integer],set[integer],set[integer])]) <=> {
    apply:(string,occMap,map[string,(occMap,addr,set[integer],set[integer],set[integer])]) =>
        map[string,(occMap,addr,set[integer],set[integer],set[integer])].
  }
  collEntry(Usage) <= {
    apply(Nm,Occs,Tb) => Tb[Nm->(Occs,aReg(-1),Use,Avoid,Conflict)] :- present(Usage,Nm,(Use,Avoid,Conflict)).
    apply(Nm,Occs,Tb) => Tb[Nm->(Occs,classifyVar(Occs),nullSet,nullSet,nullSet)].
  }

  allocateVars:(list[usageMap],integer) => (map[string,varDesc],integer,varSet).
  allocateVars(Usage,HeadArity) => (allocPerms(sort(findPerms(Usage,0,NumPerms,D1,D),compPerm),D1,NumPerms,nullSet,Lcls),NumPerms,usdRegs(Us2::set[integer],Lcls,0)) :-
    allocateHeadRegs(Usage,[],iota(1,numRegisters),[],Us0,Fr0,D0),
    findOccupiedRegs(Usage,Us0,Us1,Fr0,Fr1),
    allocateRegs(Usage,Us1,Fr1,D0,Us2,_,D1).

  -- allocate registers based on occurrences in head
  allocateHeadRegs:(list[usageMap],list[integer],list[integer],map[string,varDesc],
        list[integer],list[integer],map[string,varDesc]){}.
  allocateHeadRegs([],UsedRgs,FreeRgs,Dict,UsedRgs,FreeRgs,Dict).
  allocateHeadRegs([usage(Nm,occ(_,Occs),aReg(_),Use,Avoid,Conflict),..Tble],Ur,Fr,D,Ux,Fx,Dx) :-
    ((hArg,_,Rg) in Occs ?
      ( Rg in Fr ?
        D0 = D[Nm->varDesc(Nm,inited,isDefined,aReg(Rg),lastOcc(Occs))],
        U0 = addMem(Ur,Rg),
        F0 = delMem(Fr,Rg) |
        D0 = D[Nm->varDesc(Nm,notInited,isDefined,notAlloc,lastOcc(Occs))],
        U0=Ur,
        F0=Fr) |
      D0=D,F0=Fr,U0=Ur),
    allocateHeadRegs(Tble,U0,F0,D0,Ux,Fx,Dx).
  allocateHeadRegs([U,..L],Ur,Fr,D,Ux,Fx,Dx) :-
    allocateHeadRegs(L,Ur,Fr,D,Ux,Fx,Dx).

  avoidIota:(list[integer],integer) => list[integer].
  avoidIota(Fr,0) => Fr.
  avoidIota(Fr,Ix) => avoidIota(delMem(Fr,Ix),Ix-1).

  -- allocate registers not already spoken for - based on body
  allocateRegs:(list[usageMap],list[integer],list[integer],map[string,varDesc],
        list[integer],list[integer],map[string,varDesc]){}.
  allocateRegs([],UsedRgs,FreeRgs,Dict,UsedRgs,FreeRgs,Dict).
  allocateRegs([usage(Nm,occ(_,Occs),aReg(_),Use,Avoid,Conflict),..Tble],Ur,Fr,D,Ux,Fx,Dx) :-
    present(D,Nm,_), -- already allocated
    allocateRegs(Tble,Ur,Fr,D,Ux,Fx,Dx).
  allocateRegs([usage(Nm,occ(_,Occs),aReg(_),Use,Avoid,Conflict),..Tble],Ur,Fr,D,Ux,Fx,Dx) :-
    (lArg,_,Rg) in Occs, Rg in Fr, \+ Rg in Avoid, \+Rg in Conflict,
    allocateRegs(Tble,addMem(Ur,Rg),delMem(Fr,Rg),D[Nm->varDesc(Nm,notInited,isDefined,aReg(Rg),lastOcc(Occs))],Ux,Fx,Dx).
  allocateRegs([usage(Nm,occ(_,Occs),aReg(_),Use,Avoid,Conflict),..Tble],Ur,Fr,D,Ux,Fx,Dx) :-
    Rg in Fr, \+ Rg in Avoid, \+Rg in Conflict,
    allocateRegs(Tble,addMem(Ur,Rg),delMem(Fr,Rg),D[Nm->varDesc(Nm,notInited,isDefined,aReg(Rg),lastOcc(Occs))],Ux,Fx,Dx).
  allocateRegs([U,..L],Ur,Fr,D,Ux,Fx,Dx) :-
    allocateRegs(L,Ur,Fr,D,Ux,Fx,Dx).

  compPerm:((string,option[(integer,integer)],addr,defined),(string,option[(integer,integer)],addr,defined)){}.
  compPerm((_,some((G1,I1)),_,_),(_,some((G2,I2)),_,_)) :- (G2<G1 | G2=G1, I2<I1).

  findOccupiedRegs:(list[usageMap],list[integer],list[integer],list[integer],list[integer]){}.
  findOccupiedRegs([],Us,Us,Fr,Fr).
  findOccupiedRegs([usage(_,occ(_,Occs),_,_,_,_),..Usage],Us0,Usx,Fr,Frx) :-
    ((hArg,_,Rg) in Occs,Rg in Fr ?
      findOccupiedRegs(Usage,addMem(Us0,Rg),Usx,delMem(Fr,Rg),Frx) |
      findOccupiedRegs(Usage,Us0,Usx,Fr,Frx)).

  -- find last occurrence
  lastOcc:(list[(occForm,integer,integer)]) => option[(integer,integer)].
  lastOcc(O) => lastRgUsage(O,-1,-1).

  lastRgUsage:(list[(occForm,integer,integer)],integer,integer) => option[(integer,integer)].
  lastRgUsage([],-1,_) => none.
  lastRgUsage([],Gp,Ix) => some((Gp,Ix)).
  lastRgUsage([(_,Gp,Ix),..U],G,I) => lastRgUsage(U,Gp,Ix) :- ( Gp>G | Gp=G, Ix>I).
  lastRgUsage([_,..U],G,I) => lastRgUsage(U,G,I).

  findPerms:(list[usageMap],integer,integer,map[string,varDesc],map[string,varDesc]) =>
      list[(string,option[(integer,integer)],addr,defined)].
  findPerms([],PermNo,PermNo,D,D) => [].
  findPerms([usage(Nm,occ(_,Occs),yReg(_,Sfe),Use,Avoid,Conflict),..Tble],NoP,NoPx,D,Dx) =>
    [(Nm,lastOcc(Occs),yReg(0,Sfe),definedInRegister(Occs)),..findPerms(Tble,NoP+1,NoPx,D,Dx)].
  findPerms([usage(Nm,occ(_,Occs),voidAddr,_,_,_),..Tble],NoP,NoPx,D,Dx) =>
    findPerms(Tble,NoP,NoPx,D[Nm->varDesc(Nm,inited,definedInRegister(Occs),voidAddr,none)],Dx).
  findPerms([_,..Tble],NoP,NoPx,D,Dx) => findPerms(Tble,NoP,NoPx,D,Dx).

  definedInRegister:(list[(occForm,integer,integer)]) => defined.
  definedInRegister([]) => notDefined.
  definedInRegister([(hArg,_,_),.._]) => isDefined.
  definedInRegister([(lArg,_,_),.._]) => isDefined.
  definedInRegister([_,..O]) => definedInRegister(O).

  compPos:(option[(integer,integer)],option[(integer,integer)]){}.
  compPos(some((G1,_)),some((G2,_))) :- G1<G2.
  compPos(some((G,X1)),some((G,X2))) :- X1<X2.

  allocPerms:(list[(string,option[(integer,integer)],addr,defined)],map[string,varDesc],integer,set[integer],set[integer]) => map[string,varDesc].
  allocPerms([],M,_,Lcls,Lcls) => M.
  allocPerms([(Nm,U,yReg(_,Sfe),D),..T],M,vNo,Lc0,Lcx) =>
    allocPerms(T,M[Nm->varDesc(Nm,notInited,D,yReg(vNo,Sfe),U)],vNo-1,addMem(Lc0,vNo),Lcx).
}
