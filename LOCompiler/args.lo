lo.comp.args {
  import lo.uri.
  import lo.resources.
  import lo.

  import lo.comp.package.
  import lo.comp.errors.

  public type compOption <~ {
    debugging : logical.
    profiling : logical.
    wDir : uri.
    repoDir : uri.
    version : version.
    pkgs : list[pkg].
    dependency : logical.
  }

  private comp:(logical,logical,uri,uri,version,list[pkg],logical)<=>compOption.
  comp(d,p,w,r,v,pk,dp) <= {
    debugging = d.
    profiling = p.
    wDir = w.
    repoDir = r.
    version = v.
    pkgs = pk.
    dependency = dp.
  }

  updateDbg:(compOption,logical) => compOption.
  updateDbg(comp(_,p,w,r,v,pk,dp),d) => comp(d,p,w,r,v,pk,dp).

  updateProf:(compOption,logical) => compOption.
  updateProf(comp(d,_,w,r,v,pk,dp),p) => comp(d,p,w,r,v,pk,dp).

  updateWd:(compOption,uri) => compOption.
  updateWd(comp(d,p,_,r,v,pk,dp),w) => comp(d,p,w,r,v,pk,dp).

  updateRepoD:(compOption,uri) => compOption.
  updateRepoD(comp(d,p,w,_,v,pk,dp),r) => comp(d,p,w,r,v,pk,dp).

  updatePk:(compOption,list[pkg]) => compOption.
  updatePk(comp(d,p,w,r,v,_,dp),pk) => comp(d,p,w,r,v,pk,dp).

  updateVers:(compOption,version) => compOption.
  updateVers(comp(d,p,w,r,_,pk,dp),v) => comp(d,p,w,r,v,pk,dp).

  updateDep:(compOption,logical) => compOption.
  updateDep(comp(d,p,w,r,v,pk,_),dp) => comp(d,p,w,r,v,pk,dp).

  public parseFlags:(list[string]) => compOption.  
  parseFlags(args) => parseFlgs(args,comp(false,false,W,W,defltVersion,[],true)) :- W=cwd().

  private parseFlgs:(list[string],compOption) => compOption.
  parseFlgs([],Opts) => Opts.
  parseFlgs(["--",..Pkgs],Opts) => updatePk(Opts,locatePkgs(Opts,Pkgs)).
  parseFlgs(["-g",..args],Opts) => parseFlgs(args,updateDbg(Opts,true)).
  parseFlgs(["-p",..args],Opts) => parseFlgs(args,updateProf(Opts,true)).
  parseFlgs(["-w",W,..args],Opts) => parseFlgs(args,updateWd(Opts,resolveUri(Opts.wDir,WD))) :- uriParse(WD) %% explode(W).
  parseFlgs(["-r",W,..args],Opts) => parseFlgs(args,updateRepoD(Opts,resolveUri(Opts.wDir,D))) :- uriParse(D) %% explode(W).
  parseFlgs(["-v",V,..args],Opts) => parseFlgs(args,updateVers(Opts,vers(V))).
  parseFlgs(["-nd",..args],Opts) => parseFlgs(args,updateDep(Opts,false)).

  parseFlgs([A,..args],Opts) => parseFlgs(args,Opts) :-
    logMsg("unknown option: \(A)").

  locatePkgs:(compOption,list[string]) => list[pkg].
  locatePkgs(_,[]) => [].
  locatePkgs(Opts,[P,..L]) => [pkg(P,Opts.version),..locatePkgs(Opts,L)].
}