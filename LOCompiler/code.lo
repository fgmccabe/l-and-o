lo.comp.code.code{
  import lo.

  import lo.comp.code.instructions.
  import lo.comp.code.registers.
  import lo.comp.term.

  public assem ::= assem(term,list[instruction],list[litrl],list[(string,string,tloc)]).
  public litrl ::= litrl(string,term).

  public implementation display[assem] <= {
    disp(Seg) => dispCodeSeg(Seg).
  }

  dispCodeSeg:(assem) => ss.
  dispCodeSeg(assem(Nm,Ins,Lits,SrcMap)) => ssSeq([ss("program: "),disp(Nm),ss("\n"),ssSeq(dispIns(Ins,foldLeft((M,litrl(N,T))=>M[N->T],[],Lits))),ssSeq(anyLits(Lits))]).

  dispIns:(list[instruction],map[string,term]) => list[ss].
  dispIns([],_) => [].
  dispIns([iLbl(Lb),..L],Lits) => [ss(Lb),ss(":\n"),..dispIns(L,Lits)].
  dispIns([I,..L],Lits) => [ss("  "),showIns(I,Lits),ss("\n"),..dispIns(L,Lits)].

  anyLits:(list[litrl]) => list[ss].
  anyLits([]) => [].
  anyLits(L) => [disp(size(L)),ss(" literals\n")].

  dispLits:(list[litrl]) => list[ss].
  dispLits([]) => [].
  dispLits([litrl(Lbl,Term),..L]) => [ss(Lbl),ss(":"),disp(Term),ss("\n"),..dispLits(L)].
}
