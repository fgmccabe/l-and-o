lo.comp.canon {
  import lo.
  import lo.comp.location.
  import lo.comp.types.
  import lo.comp.package.

  public canonPkg ::= canonPkg(pkgSpec,list[pkgSpec],list[canonDef],list[canonOther]).

  public implVar:(string,canonTerm) <=> implEntry.
  implVar(Nm,T) <= {
    name = Nm.
  }

  public canonDef ::= funDef(location,string,tipe,list[constraint],list[canonRule])
           |   relDef(location,string,tipe,list[constraint],list[canonRule])
           |   grammDef(location,string,tipe,list[constraint],list[canonRule])
           |   classDef(location,string,tipe,list[constraint],list[canonRule],tipe)
           |   varDef(location,string,tipe,list[constraint],canonTerm,canonCond)
           |   otherDef(location,canonOther)
           |   typeDef(location,string,tipe,tipe)
           |   cnDefn(location,string,conEntry)
           |   implDef(location,string,string,constraint,list[constraint],list[canonTerm],canonTerm,tipe).

  public canonOther ::= integrity(location,canonCond)
           | expShow(location,canonTerm).

  public canonRule ::= clause(location,string,list[(typeMode,canonTerm)],canonCond)
            | equation(location,string,list[(typeMode,canonTerm)],canonTerm,canonCond)
            | vrDef(location,string,canonTerm,canonCond)
            | clRule(location,string,list[(typeMode,canonTerm)],canonTerm,canonCond,tipe)
            | grRule(location,string,list[(typeMode,canonTerm)],canonNT,canonNT)
            | tpRule(location,tipe).

  public canonTerm ::= v(location,string)
            | int(integer)
            | flt(float)
            | str(string)
            | apply(location,canonTerm,list[(typeMode,canonTerm)])
            | tpl(location,string,list[canonTerm])
            | whre(canonTerm,canonCond)
            | condExp(location,canonCond,canonTerm,canonTerm)
            | lambda(canonRule)
            | theta(list[canonDef],list[canonOther])
            | dot(location,canonTerm,string)
            | over(location,canonTerm,list[constraint])
            | mtd(location,string).

  public canonCond ::= trueCond
            | falseCond
            | conjCond(canonCond,canonCond)
            | disjCond(canonCond,canonCond)
            | condCond(canonCond,canonCond,canonCond)
            | unifyCond(location,canonTerm,canonTerm)
            | oneCond(canonCond)
            | negCond(canonCond)
            | forallCond(canonCond,canonCond)
            | callCond(location,canonTerm,list[(typeMode,canonTerm)])
            | isTrue(canonTerm)
            | phraseCond(location,canonNT,canonTerm,canonTerm).

  public canonNT ::= grTerms(list[(location,canonTerm,canonTerm)])
          | grCall(location,canonTerm,list[(typeMode,canonTerm)])
          | grTest(canonCond)
          | grOne(canonNT)
          | grNeg(canonNT)
          | grAhed(canonNT)
          | grDisj(canonNT,canonNT)
          | grConj(canonNT,canonNT)
          | grCond(canonNT,canonNT,canonNT).

  public implementation display[canonPkg] <= {
    disp(P) => dispPkg(P)
  }

  public implementation display[pkgSpec] <= {
    disp(PSpec) => dispPkgSpec(PSpec).
  }

  private dispPkgSpec:(pkgSpec)=>ss.
  dispPkgSpec(pkgSpec(Pk,_,_,_,_,_,_)) => disp(Pk).

  public implementation display[canonDef] <= {
    disp(C) => dispDef(C)
  }

  public implementation display[canonTerm] <= {
    disp(T) => dispTerm(T).
  }

  public implementation display[canonCond] <= {
    disp(C) => dispCond(C)
  }

  public implementation display[canonRule] <= {
    disp(C) => dispRule(C)
  }

  public implementation display[canonNT] <= {
    disp(C) => dispNT(C)
  }

  public implementation display[visibility] <= {
    disp(priVate) => ss("private").
    disp(pUblic) => ss("public").
  }

  public implementation display[canonOther] <= {
    disp(O) => dispOther(O).
  }

  private dispPkg:(canonPkg) => ss.
  dispPkg(canonPkg(Pkg,Imps,Defs,Others)) => ssSeq([ss("package: "),disp(Pkg),ss("\n"),ssSeq(dispDefs(Defs)),ss("Others:\n"),..dispOthers(Others)]).

  private dispDef:(canonDef) => ss.
  dispDef(funDef(Lc,Nm,Tp,Cx,Rules)) => ssSeq([ss(" function @ "),disp(Lc),ss("\n"),ss(Nm),ss(":"),disp(Tp),ss("\n"),ssSeq(dispRules(Rules))]).
  dispDef(relDef(Lc,Nm,Tp,Cx,Rules)) => ssSeq([ss(" relDef @ "),disp(Lc),ss("\n"),ss(Nm),ss(":"),disp(Tp),ss("\n"),ssSeq(dispRules(Rules))]).
  dispDef(grammDef(Lc,Nm,Tp,Cx,Rules)) => ssSeq([ss(" grammDef @ "),disp(Lc),ss("\n"),ss(Nm),ss(":"),disp(Tp),ss("\n"),ssSeq(dispRules(Rules))]).
  dispDef(classDef(Lc,Nm,Tp,Cx,Rules,Face)) => ssSeq([ss(" class@ "),disp(Lc),ss("\n"),ss(Nm),ss(":"),disp(Tp),ss("\n"),ssSeq(dispRules(Rules))]).
  dispDef(varDef(Lc,Nm,Tp,Cx,Exp,trueCond)) => ssSeq([ss(" var @ "),disp(Lc),ss("\n"),ss(Nm),ss(":"),disp(Tp),ss("\n"),ss(Nm),ss("="),dispTerm(Exp)]).
  dispDef(varDef(Lc,Nm,Tp,Cx,Exp,C)) => ssSeq([ss(" var @ "),disp(Lc),ss("\n"),ss(Nm),ss(":"),disp(Tp),ss("\n"),ss(Nm),ss("="),dispTerm(Exp),ss(":-"),dispCond(C)]).
  dispDef(typeDef(Lc,Nm,Tp,TpDef)) => ssSeq([ss(" type @ "),disp(Lc),ss("\n"),disp(TpDef)]).
  dispDef(otherDef(Lc,C)) => ssSeq([ss("other @" ),disp(Lc),ss("\n"),disp(C),ss("\n")]).
  dispDef(cnDefn(Lc,_,conEntry(N,Nm,Con,B))) => ssSeq([ss(" contract: @ "),disp(Lc),ss("\n"),disp(Con),ss("<~"),disp(B)]).
  dispDef(implDef(Lc,_,ImplNm,Deps,_,Hd,T,_)) =>
    ssSeq([ss(" implementation: "),ss(ImplNm),disp(Deps),ss("<="),dispTerms(Hd),ss("=>"),dispTerm(T)]).

  private dispDefs:(list[canonDef]) => list[ss].
  dispDefs([]) => [].
  dispDefs([D,..L]) => [dispDef(D),ss("\n"),..dispDefs(L)].

  private dispRule:(canonRule) => ss.
  dispRule(clause(_,Nm,Hed,trueCond)) => ssSeq([ss(Nm),dispArgs(Hed,biMode)]).
  dispRule(clause(_,Nm,Hed,Body)) => ssSeq([ss(Nm),dispArgs(Hed,biMode),ss(":-"),dispCond(Body)]).
  dispRule(equation(_,Nm,L,R,trueCond)) => ssSeq([ss(Nm),dispArgs(L,inMode),ss("=>"),dispTerm(R)]).
  dispRule(equation(_,Nm,L,R,Body)) => ssSeq([ss(Nm),dispArgs(L,inMode),ss("=>"),dispTerm(R),ss(":-"),dispCond(Body)]).
  dispRule(clRule(_,Nm,L,R,trueCond,_)) => ssSeq([dispArgs(L,biMode),ss("<="),dispTerm(R)]).
  dispRule(clRule(_,Nm,L,R,C,_)) => ssSeq([dispArgs(L,biMode),ss("<="),dispTerm(R),ss(":-"),dispCond(C)]).
  dispRule(grRule(_,Nm,L,grTerms([]),R)) => ssSeq([ss(Nm),dispArgs(L,biMode),ss("-->"),dispNT(R)]).
  dispRule(grRule(_,Nm,L,A,R)) => ssSeq([ss(Nm),dispArgs(L,biMode),ss(","),dispNT(A),ss("-->"),dispNT(R)]).
  dispRule(tpRule(_,T)) => disp(T).

  private dispRules:(list[canonRule]) => list[ss].
  dispRules([]) => [].
  dispRules([Rl,..L]) => [dispRule(Rl),ss(".\n"),..dispRules(L)].

  private dispOther:(canonOther) => ss.
  dispOther(integrity(Lc,C)) => ssSeq([ss(" assert@ "),disp(Lc),ss("\n"),dispCond(C),ss("\n")]).
  dispOther(expShow(Lc,E)) => ssSeq([ss(" show@ "),disp(Lc),ss("\n"),dispTerm(E),ss("\n")]).

  private dispOthers:(list[canonOther]) => list[ss].
  dispOthers([]) => [].
  dispOthers([Rl,..L]) => [dispOther(Rl),..dispOthers(L)].

  private showVars:(list[string]) => list[ss].
  showVars([]) => [].
  showVars([V,..L]) => [ss("all "),ss(V),..showMoreVars(L)].

  private showMoreVars:(list[string]) => list[ss].
  showMoreVars([]) => [ss("~~")].
  showMoreVars([V,..L]) => [ss(", "),ss(V),..showMoreVars(L)].

  private dispTerm:(canonTerm)=>ss.
  dispTerm(v(_,Nm)) => ss(Nm).
  dispTerm(int(Ix)) => ss(Ix::string).
  dispTerm(flt(Dx)) => ss(Dx::string).
  dispTerm(str(Sx)) => ssSeq([ss("\""),ss(Sx),ss("\"")]).
  dispTerm(apply(_,Op,Args)) => ssSeq([dispTerm(Op),dispArgs(Args,inMode)]).
  dispTerm(tpl(_,Nm,Els)) => ssSeq([ss(Nm),dispTerms(Els)]).
  dispTerm(whre(T,C)) => ssSeq([dispTerm(T),ss("@@"),dispCond(C)]).
  dispTerm(condExp(_,T,E,F)) => ssSeq([ss("("),dispCond(T),ss("?"),dispTerm(E),ss("|"),dispTerm(F),ss(")")]).
  dispTerm(dot(_,R,F)) => ssSeq([dispTerm(R),ss("."),ss(F)]).
  dispTerm(over(_,T,C)) => ssSeq([disp(C),ss("|:"),dispTerm(T)]).
  dispTerm(mtd(_,M)) => ss(M).
  dispTerm(lambda(R)) => ssSeq([ss("<"),dispRule(R),ss(">")]).
  dispTerm(theta(D,O)) => ssSeq([ss("{"),ssSeq(dispDefs(D)),ssSeq(dispOthers(O)),ss("}")]).

  private dispCond:(canonCond) => ss.
  dispCond(trueCond) => ss("true").
  dispCond(falseCond) => ss("false").
  dispCond(callCond(_,Op,Args)) => ssSeq([dispTerm(Op),dispArgs(Args,biMode)]).
  dispCond(conjCond(A,B)) => ssSeq([dispCond(A),ss(","),dispCond(B)]).
  dispCond(disjCond(A,B)) => ssSeq([ss("("),dispCond(A),ss("|"),dispCond(B),ss(")")]).
  dispCond(condCond(T,A,B)) => ssSeq([ss("("),dispCond(T),ss("?"),dispCond(A),ss("|"),dispCond(B),ss(")")]).
  dispCond(unifyCond(_,L,R)) => ssSeq([dispTerm(L),ss("="),dispTerm(R)]).
  dispCond(oneCond(A)) => ssSeq([ss("("),dispCond(A),ss(")!")]).
  dispCond(negCond(A)) => ssSeq([ss("\\+("),dispCond(A),ss(")")]).
  dispCond(isTrue(A)) => ssSeq([ss("is true ("),dispTerm(A),ss(")")]).
  dispCond(forallCond(A,B)) => ssSeq([ss("("),dispCond(A),ss("*>"),dispCond(B),ss(")")]).

  private dispNT:(canonNT) => ss.
  dispNT(grTerms(Ts)) => ssSeq([ss("["),ssSeq(dispTerminals(Ts)),ss("]")]).
  dispNT(grCall(_,Op,Args)) => ssSeq([dispTerm(Op),dispArgs(Args,biMode)]).
  dispNT(grTest(C)) => ssSeq([ss("{"),dispCond(C),ss("}")]).
  dispNT(grOne(N)) => ssSeq([ss("("),dispNT(N),ss(")!")]).
  dispNT(grNeg(N)) => ssSeq([ss("\\+("),dispNT(N),ss(")")]).
  dispNT(grAhed(N)) => ssSeq([ss("("),dispNT(N),ss(")+")]).
  dispNT(grDisj(L,R)) => ssSeq([ss("("),dispNT(L),ss("|"),dispNT(R),ss(")")]).
  dispNT(grConj(L,R)) => ssSeq([dispNT(L),ss(","),dispNT(R)]).
  dispNT(grCond(T,L,R)) => ssSeq([ss("("),dispNT(T),ss("?"),dispNT(L),ss("|"),dispNT(R),ss(")")]).

  private dispArgs:(list[(typeMode,canonTerm)],typeMode) => ss.
  dispArgs(Args,D) => ssSeq([ss("("),ssSeq(dispAs(Args,D,"")),ss(")")]).

  dispAs:(list[(typeMode,canonTerm)],typeMode,string) => list[ss].
  dispAs([],_,_) => [].
  dispAs([(M,T),..L],M,S) => [ss(S),dispTerm(T),..dispAs(L,M,", ")].
  dispAs([(M,T),..L],D,S) => [ss(S),dispTerm(T),disp(M),..dispAs(L,D,", ")].

  private dispTerms:(list[canonTerm]) => ss.
  dispTerms(Args) => ssSeq([ss("("),ssSeq(dispEls(Args,"")),ss(")")]).

  private dispEls:(list[canonTerm],string) => list[ss].
  dispEls([],_) => [].
  dispEls([E,..L],Sep) => [ss(Sep),dispTerm(E),..dispEls(L,", ")].

  private dispTerminals:(list[(location,canonTerm,canonTerm)]) => list[ss].
  dispTerminals([]) => [].
  dispTerminals([(_,_,T)]) => [dispTerm(T)].
  dispTerminals([(_,_,T),..L]) => [dispTerm(T),ss(","),..dispTerminals(L)].

  public relocate:(canonTerm,location) => canonTerm.
  relocate(v(_,Nm),Lc) => v(Lc,Nm).
  relocate(mtd(_,Nm),Lc) => mtd(Lc,Nm).
  relocate(apply(_,Op,Args),Lc) => apply(Lc,Op,Args).
}
