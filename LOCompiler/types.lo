lo.comp.types{
  import lo.

  import lo.comp.misc.

  public typeKind ::= simpleType | typeFun(integer).

  public tipe ::= anonType
        | voidType
        | thisType
        | kVar(string)
        | kFun(string,integer)
        | tVar(tipe,string,string)
        | tFun(tipe,integer,string,string)
        | tipe(string)
        | tpFun(string,integer)
        | typeExp(tipe,list[tipe])
        | tupleType(list[tipe])
        | funType(tipe,tipe)
        | predType(tipe)
        | grammarType(tipe,tipe)
        | classType(tipe,tipe)
        | typeRule(tipe,tipe)
        | tpLambda(tipe,tipe)
        | faceType(list[(string,tipe)])
        | constrained(tipe,constraint)
        | univType(tipe,tipe)
        | existType(tipe,tipe).

  public
  constraint ::= conTract(string,list[tipe],list[tipe])
        |  implementsFace(tipe,list[(string,tipe)])
        |  conCon(constraint,constraint)
        |  univCon(tipe,constraint).

  public conEntry ::= conEntry(string,string,constraint,tipe).

  public implEntry <~ { name:string}.

  public implEntry:(string,constraint) <=> implEntry.
  implEntry(Nm,C) <= {
    name = Nm.
  }

  public
  implementation display[tipe] <= {
    disp(T) => dispType(T).
  }

  public
  implementation display[constraint] <= {
    disp(C) => showConstraint(C).
  }

  private
  dispType:(tipe)=>ss.
  dispType(anonType) => ss("_").
  dispType(voidType) => ss("void").
  dispType(thisType) => ss("this").
  dispType(kVar(Nm)) => ss(Nm).
  dispType(kFun(Nm,Ar)) => ssSeq([ss(Nm),ss("/"),disp(Ar)]).
  dispType(tVar(Curr,_,_)) =>  dispType(Curr) :-  \+var(Curr).
  dispType(tVar(_,_,Id)) => ss(Id).
  dispType(tFun(Curr,_,_,_)) => dispType(Curr) :- \+var(Curr).
  dispType(tFun(_,Ar,_,Nm)) => ssSeq([ss(Nm),ss("/"),disp(Ar)]).
  dispType(tipe(Nm)) =>ss(Nm).
  dispType(tpFun(Nm,Ar)) => ssSeq([ss(Nm),ss("%"),disp(Ar)]).
  dispType(typeExp(Nm,A)) => ssSeq([dispType(Nm), ss("["),ssSeq(dispTypeEls(A)),ss("]")]).
  dispType(tupleType(A)) => ssSeq([ss("("),ssSeq(dispTypeEls(A)),ss(")")]).
  dispType(funType(A,R)) => ssSeq([dispType(A),ss("=>"), dispType(R)]).
  dispType(grammarType(A,R)) => ssSeq([dispType(A),ss("-->"), dispType(R)]).
  dispType(classType(A,R)) => ssSeq([dispType(A),ss("<=>"), dispType(R)]).
  dispType(predType(A)) => ssSeq([dispType(A),ss("{}")]).
  dispType(univType(V,Tp)) => ssSeq([ss("all "), dispType(V),.. showMoreUniv(Tp)]).
  dispType(existType(V,Tp)) => ssSeq([ss("exists "), dispType(V),.. showMoreExists(Tp)]).
  dispType(faceType(Els)) => ssSeq([ss("{ "), ssSeq(dispTypeFields(Els)), ss("}")]).
  dispType(typeRule(Hd,Bd)) => ssSeq([dispType(Hd), ss("<~"),dispType(Bd)]) :- Bd = faceType(_).
  dispType(tpLambda(Hd,Bd)) => ssSeq([dispType(Hd), ss("="),dispType(Bd)]).
  dispType(constrained(Tp,Con)) => ssSeq([showConstraint(Con),.. showMoreConstraints(Tp)]).

  showMoreUniv:(tipe) => list[ss].
  showMoreUniv(univType(V,Tp)) => [ss(","), dispType(V),.. showMoreUniv(Tp)].
  showMoreUniv(Tp) => [ss(" ~~ "),dispType(Tp)].

  showMoreExists:(tipe) => list[ss].
  showMoreExists(existType(V,Tp)) => [ss(","), dispType(V),.. showMoreExists(Tp)].
  showMoreExists(Tp) => [ss(" ~~ "),dispType(Tp)].

  showMoreConstraints:(tipe) => list[ss].
  showMoreConstraints(constrained(Tp,Con)) => [ss(","), showConstraint(Con) ,.. showMoreConstraints(Tp)].
  showMoreConstraints(Tp) => [ss("|:"),dispType(Tp)].

  showConstraint:(constraint) => ss.
  showConstraint(conTract(Nm,Args,[])) => ssSeq([ss(Nm),ss("["),ssSeq(dispTypeEls(Args)),ss("]")]).
  showConstraint(conTract(Nm,Args,Deps)) => ssSeq([ss(Nm),ss("["),ssSeq(dispTypeEls(Args)),ss("->>"),ssSeq(dispTypeEls(Deps)),ss("]")]).
  showConstraint(implementsFace(Tp,Els)) => ssSeq([dispType(Tp),ss("<~"),dispType(faceType(Els))]).
  showConstraint(conCon(Con,Extra)) => ssSeq([ssSeq(showMoreCons(Extra)),ss("|:"),showConstraint(Con)]).
  showConstraint(univCon(V,Con)) => ssSeq([ss("all "),dispType(V),..showMoreConQ(Con)]).

  -- This code is ugly. But, we dont have lambdas or higher-order programs at the moment.
  showMoreCons:(constraint) => list[ss].
  showMoreCons(conCon(A,B)) => [showConstraint(B),ss(", "),..showMoreCons(A)].
  showMoreCons(C) => [showConstraint(C)].

  showMoreConQ:(constraint) => list[ss].
  showMoreConQ(univCon(V,Con)) => [ss(", "),dispType(V),..showMoreConQ(Con)].
  showMoreConQ(Con) => [ss("~~"),showConstraint(Con)].

  dispTypeEls:(list[tipe]) => list[ss].
  dispTypeEls([]) => [].
  dispTypeEls([T,..M]) => [dispType(T),..showMoreTypes(M)].

  showMoreTypes:(list[tipe]) => list[ss].
  showMoreTypes([]) => [].
  showMoreTypes([T,..M]) => [ss(", "),dispType(T),..showMoreTypes(M)].

  dispTypeFields:(list[(string,tipe)]) => list[ss].
  dispTypeFields([]) => [].
  dispTypeFields([(K,T),..M]) => [ss(K),ss(":"),dispType(T),..showMoreTypeFields(M)].

  showMoreTypeFields:(list[(string,tipe)]) => list[ss].
  showMoreTypeFields([]) => [].
  showMoreTypeFields([(K,T),..M]) => [ss(". "),ss(K),ss(":"),dispType(T),..showMoreTypeFields(M)].

  public
  deRef:(tipe)=>tipe.
  deRef(tVar(Curr,_,_)) =>  deRef(Curr) :-  \+var(Curr) .
  deRef(tFun(Curr,_,_,_)) =>  deRef(Curr) :-  \+var(Curr) .
  deRef(T) => T.

  public
  isUnbound:(tipe){}.
  isUnbound(T) :- deRef(T) = tVar(Curr,_,_), var(Curr).
  isUnbound(T) :- deRef(T) = tFun(Curr,_,_,_), var(Curr).

  public isTypeVar:(tipe){}.
  isTypeVar(T) :- D=deRef(T), (D=tVar(_,_,_) | D=kVar(_)).

  public
  isIdenticalVar:(tipe,tipe){}.
  isIdenticalVar(tVar(_,_,Id),tVar(_,_,Id)).
  isIdenticalVar(tFun(_,_,_,Id),tFun(_,_,_,Id)).

  public isIdenticalType:(tipe,tipe){}.
  isIdenticalType(kVar(V),kVar(V)).
  isIdenticalType(kFun(V,Ar),kFun(V,Ar)).
  isIdenticalType(T1,T2) :- isIdenticalVar(T1,T2).

  public isPredType:(tipe){}.
  isPredType(univType(_,T)) :- isPredType(T).
  isPredType(existType(_,T)) :- isPredType(T).
  isPredType(constrained(T,C)) :- isPredType(T).
  isPredType(predType(_)).

  public isClassType:(tipe){}.
  isClassType(univType(_,T)) :- isClassType(T).
  isClassType(existType(_,T)) :- isClassType(T).
  isClassType(constrained(T,C)) :- isClassType(T).
  isClassType(classType(_,_)).

  public isProgramType:(tipe){}.
  isProgramType(univType(_,T)) :- isProgramType(T).
  isProgramType(existType(_,T)) :- isProgramType(T).
  isProgramType(constrained(T,_)) :- isProgramType(T).
  isProgramType(tVar(Curr,_,_)) :-  \+var(Curr), isProgramType(Curr).
  isProgramType(funType(_,_)).
  isProgramType(predType(_)).
  isProgramType(grammarType(_,_)).


  private
  collectConstraints:(tipe,list[constraint]) => list[constraint].
  collectConstraints(constrained(T,C),Cx) => collectConstraints(T,[C,..Cx]).
  collectConstraints(T,Cx) => Cx.

  public
  moveConstraints:(tipe,list[constraint],tipe){}.
  moveConstraints(constrained(T,C),[C,..C0],Inner) :-
    moveConstraints(T,C0,Inner).
  moveConstraints(T,[],T).

  public moveQuants:(tipe,list[tipe],tipe){}.
  moveQuants(univType(B,Tp),[B,..Q],Tmpl) :-
    moveQuants(Tp,Q,Tmpl).
  moveQuants(Tp,[],Tp).

  public moveConConstraints:(constraint,list[constraint],constraint){}.
  moveConConstraints(conCon(A,B),[B,..M],I) :-
    moveConConstraints(A,M,I).
  moveConConstraints(C,[],C).

  public moveConQuants:(constraint,list[tipe],constraint){}.
  moveConQuants(univCon(V,C),[V,..Vx],Cx) :-
    moveConQuants(C,Vx,Cx).
  moveConQuants(C,[],C).

  public
  newVar:(string) => tipe.
  newVar(Nm) => tVar(_,Nm,_str_gen(Nm)).

  public newTFun:(string,integer)=>tipe.
  newTFun(Nm,Ar) => tFun(_,Ar,Nm,_str_gen(Nm)).

  public
  bind:(tipe,tipe){}.
  bind(T,Tp) :- (typeKind(T)=typeKind(Tp), \+ occursIn(T,Tp), bnd(T,Tp))!.

  public implementation display[typeKind] <= {
    disp(K) => dispTpKind(K).
  }

  private dispTpKind:(typeKind) => ss.
  dispTpKind(simpleType) => ss("type").
  dispTpKind(typeFun(Ar)) => ssSeq([ss("type fun/"),disp(Ar)]).

  public typeKind:(tipe)=>typeKind.
  typeKind(tFun(_,Ar,_,_)) => typeFun(Ar).
  typeKind(kFun(_,Ar)) => typeFun(Ar).
  typeKind(tpFun(_,Ar)) => typeFun(Ar).
  typeKind(typeRule(A,_)) => typeFun(typeArity(A)).
  typeKind(tpLambda(A,_)) => typeFun(typeArity(A)).
  typeKind(_) => simpleType.

  public typeNm:(tipe) => string.
  typeNm(anonType) => "_".
  typeNm(tVar(_,_,_)) => "_".
  typeNm(voidType) => "void".
  typeNm(tipe(Nm)) => Nm.
  typeNm(tpFun(Nm,_)) => Nm.
  typeNm(typeExp(Nm,_)) => typeNm(deRef(Nm)).
  typeNm(kVar(Nm)) => Nm.
  typeNm(kFun(Nm,_)) => Nm.
  typeNm(tupleType(Els)) => "()\(length(Els))".
  typeNm(typeRule(Tp,_)) => surfaceName(deRef(Tp)).
  typeNm(tpLambda(Tp,_)) => surfaceName(deRef(Tp)).
  typeNm(univType(_,Tp)) => typeNm(Tp).
  typeNm(existType(_,Tp)) => typeNm(Tp).
  typeNm(constrained(Tp,_)) => typeNm(Tp).

  private bnd:(tipe,tipe){}.
  bnd(tVar(Tp,_,_),Tp).
  bnd(tFun(Tp,_,_,_),Tp).

  public
  occursIn:(tipe,tipe){}.
  occursIn(T,Tp) :- (T = tVar(Curr,_,Id) ? ( var(Curr) ? \+sameVar(Id,deRef(Tp)), idOccIn(Id,deRef(Tp)) | occursIn(Curr,Tp)) | false).

  private
  sameVar:(string,tipe){}.
  sameVar(Id,tVar(_,_,Id)).

  private
  idOccIn:(string,tipe){}.
  idOccIn(Id,tVar(Curr,_,_)) :- \+var(Curr), idOccIn(Id,Curr).
  idOccIn(Id,tVar(Curr,_,Id)) :- var(Curr).
  idOccIn(Id,typeExp(Op,A)) :- (idOccIn(Id,Op) | T in A, idOccIn(Id,T)).
  idOccIn(Id,tupleType(A)) :- T in A, idOccIn(Id,T).
  idOccIn(Id,funType(A,_)) :- idOccIn(Id,A).
  idOccIn(Id,funType(_,R)) :- idOccIn(Id,R).
  idOccIn(Id,predType(A)) :- idOccIn(Id,A).
  idOccIn(Id,classType(A,C)) :- idOccIn(Id,A).
  idOccIn(Id,classType(_,C)) :- idOccIn(Id,C).
  idOccIn(Id,grammarType(A,_)) :- idOccIn(Id,A).
  idOccIn(Id,grammarType(_,R)) :- idOccIn(Id,R).
  idOccIn(Id,univType(V,B)) :- \+idOccIn(Id,V), idOccIn(Id,B).
  idOccIn(Id,existType(V,B)) :- \+idOccIn(Id,V), idOccIn(Id,B).
  idOccIn(Id,faceType(L)) :- (Fld,A) in L, idOccIn(Id,A).
  idOccIn(Id,constrained(T,_)) :- idOccIn(Id,T).
  idOccIn(Id,typeRule(A,T)) :- \+idOccIn(Id,A), idOccIn(Id,T).
  idOccIn(Id,tpLambda(A,T)) :- \+idOccIn(Id,A), idOccIn(Id,T).

  public implementationName:(constraint)=>string.
  implementationName(conTract(Nm,Args,_)) =>
    formatSS(ssSeq([ss(Nm),..surfaceNames(Args,"$")])).

  public contractName:(constraint) => string.
  contractName(conTract(Nm,_,_)) =>
    pathSuffix(Nm,"$").

  surfaceNames:(list[tipe],string) => list[ss].
  surfaceNames([],_) => [].
  surfaceNames([T,..L],M) => [ss(M),ss(surfaceName(deRef(T))),..surfaceNames(L,M)].

  surfaceName:(tipe) => string.
  surfaceName(anonType) => "_".
  surfaceName(tVar(_,_,_)) => "_".
  surfaceName(voidType) => "void".
  surfaceName(tipe(Nm)) => Nm.
  surfaceName(tpFun(Nm,_)) => Nm.
  surfaceName(typeExp(Nm,_)) => surfaceName(deRef(Nm)).
  surfaceName(kVar(Nm)) => Nm.
  surfaceName(kFun(Nm,_)) => Nm.
  surfaceName(tupleType(Els)) => "()\(length(Els))".
  surfaceName(typeRule(Tp,_)) => surfaceName(deRef(Tp)).
  surfaceName(tpLambda(Tp,_)) => surfaceName(deRef(Tp)).

  public bindAT:(list[tipe],list[tipe],map[string,tipe]) => map[string,tipe].
  bindAT([],_,Q) => Q.
  bindAT(_,[],Q) => Q.
  bindAT([kVar(N),..L1],[kVar(N),..L2],Q) => bindAT(L1,L2,Q).
  bindAT([kVar(V),..L],[Tp,..TL],Q) => bindAT(L,TL,Q[V->Tp]).
  bindAT([kFun(N,Ar),..L1],[kFun(N,Ar),..L2],Q) => bindAT(L1,L2,Q).
  bindAT([kFun(N,Ar),..L1],[tpFun(Nm,Ar),..TL],Q) => bindAT(L1,TL,Q[N->tpFun(Nm,Ar)]).
  bindAT([kFun(N,Ar),..L1],[anonType,..TL],Q) => bindAT(L1,TL,Q). -- error recovery case

  public typeArity:(tipe) => integer.
  typeArity(funType(E,_)) => typeArity(E).
  typeArity(predType(E)) => typeArity(E).
  typeArity(grammarType(E,_)) => typeArity(E).
  typeArity(classType(E,_)) => typeArity(E).
  typeArity(univType(_,T)) => typeArity(T).
  typeArity(existType(_,T)) => typeArity(T).
  typeArity(constrained(T,_)) => typeArity(T).
  typeArity(tupleType(L)) => length(L).
  typeArity(faceType(F)) => length(F).

  public typeCore:(tipe)=>tipe.
  typeCore(typeRule(L,_)) => L.
  typeCore(tpLambda(L,_)) => L.
  typeCore(univType(_,Tp)) => typeCore(Tp).
  typeCore(existType(_,Tp)) => typeCore(Tp).
  typeCore(constrained(Tp,_)) => typeCore(Tp).
  typeCore(Tp) => Tp.

  public typeOp:(tipe) => tipe.
  typeOp(typeExp(Op,_)) => Op.
  typeOp(univType(_,Tp)) => typeOp(Tp).
  typeOp(existType(_,Tp)) => typeOp(Tp).
  typeOp(Tp) => Tp.

}
