lo.comp.manifest{
  import lo.
  import lo.resources.
  import lo.uri.
  import lo.comp.package.
  import lo.comp.parseutils.

  public type manifest ::= manifest(list[manifestEntry]).

  -- A manifest entry looks like:
  -- packageName : { (version:file[uri])* }

  type manifestEntry ::= manifestEntry(string,list[(version,uri,uri)]).

  -- Tokenization and parsing manifest files

  private type token ::= mAnifest | lbrce | rbrce | eqUals | stAr 
    | pkgId(string) | utok(list[integer]).

  tokens:(list[token]) --> list[integer].
  tokens(Toks) --> spaces(), moreToks(Toks).

  moreToks:(list[token]) --> list[integer].
  moreToks([]) --> eof.
  moreToks([Tok,..More]) --> token(Tok), tokens(More).

  token:(token) --> list[integer].
  token(mAnifest) --> "manifest".
  token(lbrce) --> "{".
  token(rbrce) --> "}".
  token(start) --> "*".
  token(colon) --> ":".
  token(eqUals) --> "=".
  token(utok(Seq)) --> "[", stringText(Seq), "]".
  token(pkgId(Nm)) --> pkgIden(Nm).

  stringText:(list[integer]) --> list[integer].
  stringText([]) --> [].
  stringText([C,..More]) --> "\\", quote(C), stringText(More).
  stringText([C,..More]) --> [C], { C \= 0c] }, stringText(More).

  quote:(integer) --> list[integer].
  quote(0c\n) --> "n".
  quote(0c") --> "\"".
  quote(0c') --> "'".
  quote(0c\t) --> "t".
  quote(C) --> [C].

  public readManifest:(uri) => manifest.
  readManifest(u) => m :- parseManifest(m) %% getResource(u).

  public flushManifest:(uri,manifest){}.
  flushManifest(Root,M) :-
    putResource(Root,flatten(disp(M))).

  -- Parsing a manifest from tokens
  public parseManifest:(manifest) --> list[integer].
  parseManifest(M) --> tokens(TL), { pManifest(M) %% TL}.

  pManifest:(manifest) --> list[token].
  pManifest(manifest(E)) --> [mAnifest, lbrce], entries(E), [rbrce].

  entries:(list[manifestEntry]) --> list[token].
  entries([Entry,..More]) --> entry(Entry), entries(More).
  entries([]) --> [].

  entry:(manifestEntry) --> list[token].
  entry(manifestEntry(Pk,Versions)) --> [pkgId(Pk), colon, lbrce], versions(Versions), [rbrce].

  versions:(list[(version,uri,uri)]) --> list[token].
  versions([V,..More]) --> version(V), versions(More).
  versions([]) --> [].

  version:((version,uri,uri)) --> list[token].
  version((V,Uri,CodeUri)) --> versionName(V), [equals, pkgId(F), utok(U)], { uriParse(Uri)%%U, uriParse(CodeUri)%%explode(F) }.

  versionName:(version) --> list[token].
  versionName(defltVersion) --> [stAr].
  versionName(vers(S)) --> [pkgId(S)].

  -- Displaying a manifest
  public implementation display[manifest] .. {
    disp(manifest(E)) => ssSeq([ss("manifest"),ss("{\n"),ssSeq(showEntries(E)),ss("}\n")]).
  }

  showEntries:(list[manifestEntry]) => list[ss].
  showEntries([]) => [].
  showEntries([E,..L]) => [showEntry(E),..showEntries(L)].

  showEntry:(manifestEntry) => ss.
  showEntry(manifestEntry(Pk,Versions)) => ssSeq([ss("  "),ss(Pk),ss(":{\n"),ssSeq(showVersions(Versions)),ss("  }\n")]).

  showVersions:(list[(version,uri,uri)]) => list[ss].
  showVersions([]) => [].
  showVersions([V,..M]) => [showVersion(V),..showVersions(M)].

  showVersion:((version,uri,uri)) => ss.
  showVersion((V,SrcUri,CodeUri)) => ssSeq([ss("    "),disp(V),ss("="),disp(CodeUri),ss("["),disp(SrcUri),ss("]\n")]).

  -- Simple API to manipulate entries in a mAnifest

  public locateVersion:(manifest,string,version,version,uri,uri){}.
  locateVersion(manifest(Entries),Pkg,Vers,Act,SrcUri,CodeUri) :-
    manifestEntry(Pkg,V) in Entries,
    getVersion(Vers,Act,V,SrcUri,CodeUri).

  getVersion:(version,version,list[(version,uri,uri)],uri,uri){}.
  getVersion(Vers,Vers,V,SrcUri,CodeUri) :- (Vers,SrcUri,CodeUri) in V!.
  getVersion(defltVersion,Act,V,SrcUri,CodeUri) :- (Act,SrcUri,CodeUri) in V!.

  public addToManifest:(manifest,uri,string,version,uri) => manifest.
  addToManifest(manifest(M),SrcUri,Pkg,Version,CodeUri) => manifest(addEntry(M,SrcUri,Pkg,Version,CodeUri)).

  addEntry:(list[manifestEntry],uri,string,version,uri) => list[manifestEntry].
  addEntry([],SrcUri,Pkg,Version,CodeUri) => [(manifestEntry(Pkg,[(Version,SrcUri,CodeUri)]))].
  addEntry([manifestEntry(Pkg,Vers),..E],SrcUri,Pkg,Version,CodeUri) => [manifestEntry(Pkg,addVersion(Vers,SrcUri,Version,CodeUri)),..E].
  addEntry([E,..M],SrcUri,Pkg,Version,CodeUri) => [E,..addEntry(M,SrcUri,Pkg,Version,CodeUri)].

  addVersion:(list[(version,uri,uri)],uri,version,uri) => list[(version,uri,uri)].
  addVersion([],SrcUri,Vers,CodeUri) => [(Vers,SrcUri,CodeUri)].
  addVersion([(Vers,_,_),..V],SrcUri,Vers,CodeUri) => [(Vers,SrcUri,CodeUri),..V] . -- replace version
  addVersion([V,..M],SrcUri,Vers,CodeUri) => [V,..addVersion(M,SrcUri,Vers,CodeUri)].
}