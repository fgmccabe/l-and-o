lo.comp.macro{
  import lo.
  import lo.comp.location.
  import lo.comp.ast.
  import lo.comp.abstract.
  import lo.comp.wff.
  import lo.comp.errors.

  public macroRewrite:(list[ast],list[ast]){}.
  macroRewrite([],[]).
  macroRewrite([St,..More],Stmts) :-
    isUnary(St,"public",_,Inner),
    isAlgebraicTypeDef(Inner,Lc,Quants,Constraints,Head,Body),
    convertAlgebraic(Lc,markPublic,Quants,Constraints,Head,Body,Stmts,S0),
    macroRewrite(More,S0).
  macroRewrite([St,..More],Stmts) :-
    isUnary(St,"private",_,Inner),
    isAlgebraicTypeDef(Inner,Lc,Quants,Constraints,Head,Body),
    convertAlgebraic(Lc,markPrivate,Quants,Constraints,Head,Body,Stmts,S0),
    macroRewrite(More,S0).
  macroRewrite([St,..More],Stmts) :-
    isAlgebraicTypeDef(St,Lc,Quants,Constraints,Head,Body),
    convertAlgebraic(Lc,noMark,Quants,Constraints,Head,Body,Stmts,S0),
    macroRewrite(More,S0).
  macroRewrite([St,..More],[St,..Stmts]) :-
    isContractSpec(St,_,Quants,Constraints,Con,Els),
    generateAnnotations(Els,Quants,[Con,..Constraints],Stmts,S0),
    macroRewrite(More,S0).
  macroRewrite([St,..More],[St,..Stmts]) :-
    macroRewrite(More,Stmts).

  private
  type mark ::= markPublic | markPrivate | noMark.

  private mark:(mark,location,ast,ast){}.
  mark(markPublic,Lc,Stmt,unary(Lc,"public",Stmt)).
  mark(markPrivate,Lc,Stmt,unary(Lc,"private",Stmt)).
  mark(noMark,_,Stmt,Stmt).

  -- super simple conversion. Maybe later add support for auto generation of interfaces

  convertAlgebraic:(location,mark,list[ast],list[ast],ast,ast,list[ast],list[ast]){}.
  convertAlgebraic(Lc,Mark,Quants,Constraints,Head,Body,[TypeRule,..Elements],Tail) :-
    algebraicFace(Body,[],Els),
    isBraceTuple(Face,Lc,Els),
    typeRule(Lc,Quants,Constraints,Head,Face,FaceRule),
    mark(Mark,Lc,FaceRule,TypeRule),
    convertConstructors(Body,Head,Mark,Quants,Constraints,Elements,Tail).

  convertConstructors:(ast,ast,mark,list[ast],list[ast],list[ast],list[ast]){}.
  convertConstructors(Pair,Head,Mark,Quants,Constraints,Elements,Tail) :-
    isBinary(Pair,"|",_,L,R),
    convertConstructors(L,Head,Mark,Quants,Constraints,Elements,L1),
    convertConstructors(R,Head,Mark,Quants,Constraints,L1,Tail).
  convertConstructors(Term,Head,Mark,Quants,Constraints,Elements,Tail) :-
    convertConstructor(Term,Head,Mark,Quants,Constraints,Elements,Tail).

  convertConstructor:(ast,ast,mark,list[ast],list[ast],list[ast],list[ast]){}.
  convertConstructor(Nm,Tp,Mark,Quants,Constraints,[TpRule,BodyRule,..Tail],Tail) :-
    isIden(Nm,Lc,_),
    wrapConstraints(Constraints,Lc,Tp,TpCon),
    wrapQuants(Quants,Lc,TpCon,QTp),
    mark(Mark,Lc,binary(Lc,":",Nm,QTp),TpRule),
    bodyRule(Lc,Nm,[],BodyRule).
  convertConstructor(Con,Tp,Mark,Quants,Constraints,[TpRule,BodyRule,..Tail],Tail) :-
    isRoundTerm(Con,Lc,Op,ArgTypes), /* Construct con:(T1,..,Tn)<=>Tp */
    classType(Lc,ArgTypes,Tp,ClassType),
    wrapConstraints(Constraints,Lc,ClassType,CTp),
    wrapQuants(Quants,Lc,CTp,QTp),
    mark(Mark,Lc,binary(Lc,":",Op,QTp),TpRule),
    genAnonArgs(ArgTypes,Args),       /* Construct con(_,..,_) <= thing */
    bodyRule(Lc,roundTerm(Lc,Nm,Args),[],BodyRule).
  convertConstructor(Con,Tp,Mark,Quants,Constraints,[TpRule,BodyRule,..Tail],Tail) :-
    isBraceTerm(Con,_,Op,FldTps),
    wrapConstraints(Constraints,Lc,binary(Lc,"<=>",braceTuple(Lc,FldTps),Tp),CTp),
    wrapQuants(Quants,Lc,CTp,QTp),
    mark(Mark,Lc,binary(Lc,":",Op,QTp),TpRule),
    genFieldArgs(FldTps,Args,Defs),
    bodyRule(Lc,roundTerm(Lc,Op,Args),Defs,BodyRule).

  algebraicFace:(ast,list[ast],list[ast]){}.
  algebraicFace(T,SoFar,Face) :-
    isBinary(T,"|",_,L,R),
    algebraicFace(L,SoFar,SF),
    algebraicFace(R,SF,Face).
  algebraicFace(T,SoFar,SoFar) :-
    isRoundTerm(T,_,_,_).
  algebraicFace(T,Face,Face) :-
    isIden(T,_,_).
  algebraicFace(T,SoFar,Face) :-
    isBraceTerm(T,_,_,Args),
    pickupFields(Args,SoFar,Face).

  pickupFields:(list[ast],list[ast],list[ast]){}.
  pickupFields([],Face,Face).
  pickupFields([T,..M],SF,Face) :-
    isBinary(T,":",Lc,L,R),
    isIden(L,_,Nm),
    checkSoFar(Nm,R,SF,SF0),
    pickupFields(M,[T,..SF0],Face).
  pickupFields([T,..M],SF,Face) :-
    reportError("invalid type field: $(T)",T.loc),
    pickupFields(M,SF,Face).

  checkSoFar:(string,ast,list[ast],list[ast]){}.
  checkSoFar(_,_,[],[]).
  checkSoFar(Nm,T,[P,..L],L) :-
    isBinary(P,":",Lc,NN,RR),
    isIden(NN,_,Nm),
    (sameTerm(RR,T) | reportError("field $(Nm) : $(T) must be identical to: $(RR)",Lc)).
  checkSoFar(Nm,T,[P,..L],[P,..LL]) :-
    checkSoFar(Nm,T,L,LL).

  typeRule:(location,list[ast],list[ast],ast,ast,ast){}.
  typeRule(Lc,Quants,Constraints,Hd,Body,Rule) :-
    wrapConstraints(Constraints,Lc,binary(Lc,"<~",Hd,Body),ConRl),
    wrapQuants(Quants,Lc,ConRl,Rule).

  wrapQuants:(list[ast],location,ast,ast){}.
  wrapQuants([],_,Rule,Rule).
  wrapQuants(Q,Lc,Rl,unary(Lc,"all",binary(Lc,"~~",QV,Rl))) :-
    listComma(Q,Lc,QV).

  wrapConstraints:(list[ast],location,ast,ast){}.
  wrapConstraints([],_,Tp,Tp).
  wrapConstraints(Con,Lc,Tp,binary(Lc,"|:",CTp,Tp)) :-
    listComma(Con,Lc,CTp).

  listComma:(list[ast],location,ast){}.
  listComma([T],_,T).
  listComma([T,..R],Lc,binary(Lc,",",T,RR)) :-
    listComma(R,Lc,RR).

  genAnonArgs:(list[ast],list[ast]){}.
  genAnonArgs([],[]).
  genAnonArgs([T,..M],[iden(T.loc,"_"),..A]) :- genAnonArgs(M,A).

  genFieldArgs:(list[ast],list[ast],list[ast]){}.
  genFieldArgs([],[],[]).
  genFieldArgs([F,..M],[V,..R],[binary(Lc,"=",L,V),..AR]) :-
    isBinary(F,":",Lc,L,_),
    V = iden(Lc,_str_gen("_")),
    genFieldArgs(M,R,AR).

  hasType:(location,string,ast,ast){}.
  hasType(Lc,Nm,Tp,binary(Lc,":",iden(Lc,Nm),Tp)).

  classType:(location,list[ast],ast,ast){}.
  classType(Lc,Args,Res,binary(Lc,"<=>",A,Res)) :- isRoundTuple(A,Lc,Args).

  funType:(location,list[ast],ast,ast){}.
  funType(Lc,Args,Res,binary(Lc,"=>",A,Res)) :- isRoundTuple(A,Lc,Args).

  genericType:(location,ast,list[ast],ast){}.
  genericType(Lc,Nm,Args,squareTerm(Lc,Nm,Args)).

  bodyRule:(location,ast,list[ast],ast){}.
  bodyRule(Lc,Hd,Els,braceTerm(Lc,Hd,Els)).

  generateAnnotations:(list[ast],list[ast],list[ast],list[ast],list[ast]){}.
  generateAnnotations([],_,_,Stmts,Stmts).
  generateAnnotations([Def,..Els],Quants,Constraints,[binary(Lc,":",N,MTp),..Stmts],S0) :-
    isBinary(Def,":",Lc,N,Tp),
    wrapConstraints(Constraints,Lc,Tp,CTp),
    wrapQuants(Quants,Lc,CTp,MTp),
    generateAnnotations(Els,Quants,Constraints,Stmts,S0).
  generateAnnotations([_,..Els],Quants,Constraints,Stmts,S0) :- -- ignore things like assertions
    generateAnnotations(Els,Quants,Constraints,Stmts,S0).
}